:Class index : MiPage

    ∇ Compose;input;inputArea;outputArea
      :Access Public
      Add _.Style'#num,#dir'('border-style' 'solid' 'margin' '0.5em' 'padding' '10px')
      Add _.title'IUI - The Involution UI'
     
      input←('N'('type' 'range')('min' 1)('max' 20)('value' 1))New _.EditField''
     
      inputArea←New _.InputGrid'N:'input
      (outputArea←New _.StackPanel).Add¨'#num' '#dir'{⍺ New _.pre ⍵}¨1 '→'
      outputArea.Horizontal←1
      (Add _.StackPanel).Add¨inputArea outputArea
     
      input.On'change' 'Callback'
    ∇

    ∇ r←Callback
      :Access Public
      :If (,83)≡⎕DR¨N←#.Strings.tonum _value ⍝ single integer?
          r←⊃,/'#num' '#dir'Replace¨0 1 involute¨N
      :Else
          r←Execute _JSS.Alert'Please enter a single integer'
      :EndIf
    ∇

      involute←{
          ⍺←0                        ⍝ default: return numbers
          moves←(¯1+2×⍵)⍴m←1 ⍵,-1 ⍵  ⍝ sequence of 1 ⍵ ¯1 (-⍵)
          repeat←1↓2/⌽⍳⍵             ⍝ number of times to repeat each move
          path←+\repeat/moves        ⍝ path through matrix (viewed as a list)
          ⍺≡0:⍵ ⍵⍴⍋path              ⍝ return numbers if ⍺=0
          charmoves←repeat/'→↓←↑'[m⍳moves] ⍝ else map moves to arrows
          r←⍵ ⍵⍴(charmoves@path)(⍵×⍵)⍴⍺    ⍝ insert arrows along path
          ((2×⍵)⍴1 0)\r              ⍝ insert alternate blank columns
      }

:EndClass
